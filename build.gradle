/*
 * Copyright 2014 toxbee.se
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

task wrapper( type: Wrapper ) {
	gradleVersion = '1.10'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'maven'

	build.finalizedBy uploadArchives

	group = "se.toxbee.fimpl"

	uploadArchives {
		def gitroscm = "https://github.com/toxbee/fimpl-core"

		repositories.mavenDeployer {
			repository( url: uri( "../maven-deploy" ) )
			pom {
				project {
					inceptionYear '2014'
					scm {
						url "${gitroscm}";
						connection "${gitroscm}";
					}

					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
				}
			}
		}
	}

	// Used for testing:
	apply plugin: 'groovy'
	repositories {
		mavenCentral()
	}

	dependencies {
		testCompile "org.spockframework:spock-core:0.7-groovy-2.0"

	}
}

project( ':fimpl-common' ) {
	version = '0.1'
}

project( ':fimpl-annotate' ) {
	dependencies {
		compile project( ':fimpl-common' )
	}
	version = '0.1'
}

project( ':fimpl-api' ) {
	dependencies {
		compile project( ':fimpl-common' )
	}
	version = '0.1'
}

project( ':fimpl-metainf' ) {
	dependencies {
		compile project( ':fimpl-api' )
	}
	version = '0.1'
}